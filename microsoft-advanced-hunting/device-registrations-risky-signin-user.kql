// Query to get device registrations around the time of risky sign-ins for a user
let startTime=ago(7d);
let endTime=ago(0d);
let AlertUPN = "johndoe@example.com"; 
AuditLogs
  | where TimeGenerated between (startTime .. endTime)
  | where OperationName =~ "Add registered owner to device" 
  | where Identity =~ "Device Registration Service"
  | extend UserPrincipalName = tostring(TargetResources[0].userPrincipalName)
  | where UserPrincipalName == AlertUPN
  | extend DeviceObjectId = trim('"', tostring(TargetResources[0].modifiedProperties[0].newValue))
  | extend DeviceDisplayName = trim('"', tostring(TargetResources[0].modifiedProperties[1].newValue))
  | project DeviceRegistrationTimestamp=TimeGenerated, CorrelationId, UserPrincipalName, DeviceObjectId, DeviceDisplayName
| join (SigninLogs
    | where RiskLevelDuringSignIn != "none"
    | summarize by RiskyLogonTime = TimeGenerated, UserPrincipalName, IPAddress 
    )
    on UserPrincipalName