// Query for sessions which contain logons from multiple platforms and countries. This can help identify scenarios of stolen session tokens being used from new devices, countries.  
// Returns results where same session ID was observed from two different countries and specific workstation OS platforms or three different OS versions (assumes a user typically logs in from two devices - a personal phone and a workstation).
let startTime=ago(30d);
let endTime=ago(0d);
let OSCountThreshold = 2;
let CountryCountThreshold = 1;
AADSignInEventsBeta
| where Timestamp between (startTime .. endTime)
| extend NormalisedOS = extract("([a-zA-Z]+)", 1,OSPlatform)
| summarize makeset(UserAgent),make_set(IPAddress),makeset(DeviceName), make_set(Country), CountryCount = dcount(Country), OSCount = dcount(OSPlatform), OSPlatformSet= makeset(OSPlatform), NormOSSet = makeset(NormalisedOS) by AccountUpn,SessionId
| where CountryCount > CountryCountThreshold
| where (OSCount > OSCountThreshold or NormOSSet has_all ("windows","macos") or NormOSSet has_all ("windows", "linux") or NormOSSet has_all ("macos", "linux")) 