// Query to find other users who have recieved email from the same sender, following an 'Email reported by user as malware or phish' alert
let startTime=ago(7d);
let endTime=ago(0d);
AlertInfo
| where Timestamp between (startTime .. endTime)
| where Title contains "Email reported by user as malware or phish"
| join AlertEvidence on AlertId
| extend ParsedFields = parse_json(AdditionalFields)
| extend SenderFromAddress = tostring(ParsedFields.Sender)
| extend ReportedBy = AccountUpn
| where isnotempty(SenderFromAddress)
| summarize by Title, SenderFromAddress, ReportedBy, Timestamp
| join (EmailEvents
| where DeliveryAction != "Blocked" or LatestDeliveryAction != "Blocked"
) on SenderFromAddress
| join kind = leftouter (EmailUrlInfo| summarize by Url, NetworkMessageId) on NetworkMessageId
| join kind = leftouter (EmailAttachmentInfo| summarize by FileName, FileType, NetworkMessageId) on NetworkMessageId
| summarize Urls=makeset(Url), Files = makeset(pack('File:',FileName,'Type:', FileType)), DeliveryActions = makeset(DeliveryAction), LatestDeliveryActions = makeset(LatestDeliveryAction), ThreatDetails = makeset(pack("ThreatName:", ThreatNames,"ThreatType:", ThreatTypes, "DetectionMethods:", DetectionMethods,"ConfidenceLevel:", ConfidenceLevel)) by AlertTime = Timestamp,Title, RecipientEmailAddress, SenderFromAddress,Subject, EmailReceivedTime = Timestamp1, ReportedBy
| where ReportedBy != RecipientEmailAddress