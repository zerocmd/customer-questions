//This query determines the closest logon on a device before a security alert which can identify the user involved and the origin device. This can be useful to identify the user involved in alerts that don't contain user information such as Defender for Identity alerts or alerts for shared account activity. 
let startTime=ago(7d);
let endTime=ago(0d);
let AlertDeviceName = "workstation";
SecurityAlert
| where TimeGenerated between (startTime .. endTime)
| mv-expand todynamic(Entities)
| where Entities.Type == "host"
| extend DeviceName = tolower(tostring(Entities.HostName)), AlertTime = TimeGenerated, AlertDeviceId = tostring(Entities.MdatpDeviceId)
| where DeviceName contains AlertDeviceName
| summarize by AlertName, DeviceName, AlertTime, SystemAlertId, AlertDeviceId 
| join (DeviceLogonEvents
    | where TimeGenerated between (startTime .. endTime)
    | where ActionType == "LogonSuccess"
    | summarize
        by
        LoggedonUser = AccountName,
        LoggedonUserDomain = AccountDomain,
        LogonTime = TimeGenerated,
        LogonDeviceId = DeviceId,
        DeviceName=tolower(DeviceName),
        RemoteDeviceName,
        RemoteIP,
        ActionType,
        SourceSystem)
    on DeviceName
| extend TimeDiff = datetime_diff('second', AlertTime, LogonTime)
| where TimeDiff > 0 // Only return successful logons before the alert time
| summarize arg_min(TimeDiff, *) by AlertName, SystemAlertId
| project-away DeviceName1, TimeDiff